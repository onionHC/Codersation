apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'application'
apply from: 'https://github.com/valkolovos/gradle_cobertura/raw/master/repo/gradle_cobertura/gradle_cobertura/1.2.1/coberturainit.gradle'


defaultTasks 'clean', 'test', 'cobertura'

[compileJava, compileTestJava, compileTestGroovy]*.options*.encoding = 'UTF-8'

repositories {
  mavenCentral()
}

dependencies {
  groovy 'org.codehaus.groovy:groovy:2.0.5'
  testCompile 'junit:junit-dep:4.11'
  testCompile 'org.hamcrest:hamcrest-core:1.3'
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testCompile 'org.yaml:snakeyaml:1.10'
}

test {
  jvmArgs '-XX:-UseSplitVerifier'
}

mainClassName = 'net.codersation.vendingmachine.Main'

eclipse {
  project.name = 'VendingMachine_irof'
  classpath {
   downloadSources=true
  }
  jdt.file.withProperties { properties ->
    properties.put('org.eclipse.jdt.core.formatter.lineSplit', '120')
  }
}

eclipseJdt << {
  file('.settings/org.eclipse.core.resources.prefs').withWriter {
    it << 'eclipse.preferences.version=1\n'
    it << 'encoding/<project>=UTF-8\n'
  }
  file('.settings/org.eclipse.jdt.groovy.core.prefs').withWriter {
    it << 'eclipse.preferences.version=1\n'
    it << 'groovy.compiler.level=20\n'
  }
}
cleanEclipseJdt << {
  file('.settings/org.eclipse.core.resources.prefs').delete()
  file('.settings/org.eclipse.jdt.groovy.core.prefs').delete()
}

test.exclude '**/*$*'

task wrapper(type: Wrapper) {
  gradleVersion = '1.3'
}
